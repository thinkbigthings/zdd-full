
plugins {
    id "com.heroku.sdk.heroku-gradle" version "2.0.0"
}


// TODO this isn't appropriate for all subprojects, just the Java ones (not React)
subprojects {

    apply plugin: 'java' // core plugin provided by gradle
    
    group = 'org.thinkbigthings.zdd'
    version = '1.0-SNAPSHOT'

    sourceCompatibility = JavaVersion.VERSION_15

    repositories {
        mavenCentral()
    }

    dependencies {

        // the intent here is to replace the version of jackson pulled in by spring boot
        // and to make the same version of jackson available to subprojects
        implementation 'com.fasterxml.jackson.core:jackson-core:2.12.0'
        implementation 'com.fasterxml.jackson.core:jackson-annotations:2.12.0'
        implementation 'com.fasterxml.jackson.core:jackson-databind:2.12.0'

        testImplementation 'org.junit.jupiter:junit-jupiter:5.7.0'
        testImplementation 'org.mockito:mockito-junit-jupiter:3.6.28'
    }

    test {
        useJUnitPlatform()
    }


    // use preview features
    tasks.withType(JavaCompile) {
        options.compilerArgs += "--enable-preview"
        options.compilerArgs += "-Xlint:unchecked"
    }
    tasks.withType(Test) {
        jvmArgs += "--enable-preview"
    }
    tasks.withType(JavaExec) {
        jvmArgs += '--enable-preview'
    }
}

// use heroku gradle plugin
// https://github.com/heroku/heroku-gradle
heroku {

    appName = project.hasProperty('app') ? project.property('app') : 'stage-zdd-full';
    jdkVersion = 15
    includes = ["server/build/libs/server-1.0-SNAPSHOT.jar"]
    includeBuildDir = false

    processTypes(
        // The \$ is to escape $ for build.gradle, don't need the slash when running the command locally
        release: "java --enable-preview -Dspring.datasource.url='\${JDBC_DATABASE_URL}&autosave=conservative' -Dspring.profiles.active=migration -jar server/build/libs/server-1.0-SNAPSHOT.jar",
        web:     "java --enable-preview -Dspring.datasource.url='\${JDBC_DATABASE_URL}&autosave=conservative' -Dserver.port='\${PORT}'           -jar server/build/libs/server-1.0-SNAPSHOT.jar"
    )
}

task copyWebApp(type: Copy) {
    description 'copies built files from reactjs to the server'
    dependsOn ':reactjs:build'
    dependsOn ':server:classes'
    from 'reactjs/build'
    into 'server/build/resources/main/static'
}

task build () {
    dependsOn('copyWebApp')
    dependsOn(':server:bootJar')
}

project(':perf') {
    dependencies {
        implementation project(':dto')
    }
}

project(':server') {
    dependencies {
        implementation project(':dto')
    }
}
