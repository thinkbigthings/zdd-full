
plugins {
    id "com.heroku.sdk.heroku-gradle" version "2.0.0"
}


// TODO this isn't appropriate for all subprojects, just the Java ones (not React)
subprojects {

    apply plugin: 'java' // core plugin provided by gradle
    
    group = 'org.thinkbigthings.zdd'
    version = '1.0-SNAPSHOT'

    sourceCompatibility = JavaVersion.VERSION_15

    repositories {
        mavenCentral()
    }

    dependencies {

        // the intent here is to replace the version of jackson pulled in by spring boot
        // and to make the same version of jackson available to subprojects
        implementation 'com.fasterxml.jackson.core:jackson-core:2.12.0-rc1'
        implementation 'com.fasterxml.jackson.core:jackson-annotations:2.12.0-rc1'
        implementation 'com.fasterxml.jackson.core:jackson-databind:2.12.0-rc1'

        testImplementation 'org.junit.jupiter:junit-jupiter:5.6.2'
        testImplementation 'org.mockito:mockito-junit-jupiter:3.4.6'
    }

    test {
        useJUnitPlatform()
    }


    // use preview features
    tasks.withType(JavaCompile) {
        options.compilerArgs += "--enable-preview"
        options.compilerArgs += "-Xlint:unchecked"
    }
    tasks.withType(Test) {
        jvmArgs += "--enable-preview"
    }
    tasks.withType(JavaExec) {
        jvmArgs += '--enable-preview'
    }
}

// use heroku gradle plugin
// https://github.com/heroku/heroku-gradle
heroku {

    appName = "zdd-full" // should be configurable so we don't deploy to wrong environment

    includes = ["server/build/libs/server-1.0-SNAPSHOT.jar"]
    includeBuildDir = false

    jdkVersion = 15

    processTypes(
        // escape the $ for gradle
        web: "java --enable-preview -Dserver.port=\$PORT -jar server/build/libs/server-1.0-SNAPSHOT.jar"
    )
}

task copyWebApp(type: Copy) {
    description 'copies built files from reactjs to the server'
    dependsOn ':reactjs:build'
    dependsOn ':server:classes'
    from 'reactjs/build'
    into 'server/build/resources/main/static'
}

task runAll () {
    dependsOn('copyWebApp')
    dependsOn(':server:bootRun')
}

task build () {
    dependsOn('copyWebApp')
    dependsOn(':server:bootJar')
}

project(':perf') {
    dependencies {
        implementation project(':dto')
    }
}

project(':server') {
    dependencies {
        implementation project(':dto')
    }
}
