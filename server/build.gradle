
buildscript {
    dependencies {
        classpath 'org.postgresql:postgresql:42.2.8'
    }
}

plugins {
    id 'org.springframework.boot' version '2.3.2.RELEASE'
    id 'org.flywaydb.flyway' version '6.0.4'
    id 'jacoco' // core plugin provided by gradle
}

// https://docs.gradle.org/current/userguide/jacoco_plugin.html
jacoco {
    toolVersion = "0.8.6"
//    reportsDir = file("$buildDir/customJacocoReportDir")
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination file("${buildDir}/reports/jacoco/html")
    }
}

flyway {
    
    // get properties for server and flyway from the same place 
    def props = new Properties()
    file("application.properties").withInputStream { props.load(it) }
    
    url = props.getProperty("spring.datasource.url") // 'jdbc:postgresql://localhost:5555/app'
    user = props.getProperty("spring.datasource.username") //'postgres'
    password = props.getProperty("spring.datasource.password") //'postgres'
    schemas = ['public']
}

dependencies {

    // implementation 'javax.inject:javax.inject:1'
    implementation 'org.postgresql:postgresql:42.2.8'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa:2.3.2.RELEASE'
    implementation 'org.springframework.boot:spring-boot-starter-web:2.2.6.RELEASE'
    implementation 'org.springframework.boot:spring-boot-starter-actuator:2.3.2.RELEASE'
    implementation 'org.springframework.boot:spring-boot-starter-security:2.3.2.RELEASE'

    // JAXB was removed in Java 11 so needs to be included here
    implementation 'javax.xml.bind:jaxb-api:2.3.1'
}
 
// clear the database, rebuild, and run with fresh database
task cleanRun(type: GradleBuild) {
    tasks = ['clean', 'flywayClean', 'flywayMigrate', 'bootRun']
}