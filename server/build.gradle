
buildscript {
    dependencies {
        classpath 'org.postgresql:postgresql:42.2.18'
    }
}

plugins {
    id 'org.springframework.boot' version '2.4.0'
    id 'jacoco' // core plugin provided by gradle
}

// https://docs.gradle.org/current/userguide/jacoco_plugin.html
jacoco {
    toolVersion = "0.8.6"
//    reportsDir = file("$buildDir/customJacocoReportDir")
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination file("${buildDir}/reports/jacoco/html")
    }
}

dependencies {

    implementation 'org.postgresql:postgresql:42.2.8'

    implementation 'org.springframework.boot:spring-boot-starter-data-jpa:2.4.0'
    implementation 'org.springframework.boot:spring-boot-starter-web:2.4.0'
    implementation 'org.springframework.boot:spring-boot-starter-actuator:2.4.0'
    implementation 'org.springframework.boot:spring-boot-starter-security:2.4.0'
    implementation 'org.springframework.boot:spring-boot-starter-validation:2.4.0'

    implementation 'org.springframework.session:spring-session-jdbc:2.4.0'

    // JAXB was removed in Java 11 so needs to be included here
    implementation 'javax.xml.bind:jaxb-api:2.3.1'

    testImplementation 'org.springframework.boot:spring-boot-starter-test:2.4.0'
    testImplementation 'com.github.javafaker:javafaker:1.0.2'

}

// integration test by tags,

// inspired by https://stackoverflow.com/questions/40891867/junit5-tag-specific-gradle-task

def test = tasks.named("test") {
    useJUnitPlatform {
        excludeTags "integration"
    }
}

def integrationTest = tasks.register("integrationTest", Test) {
    useJUnitPlatform {
        includeTags "integration"
    }
    shouldRunAfter test
}

tasks.named("check") {
    dependsOn integrationTest
}
